plugins {
    id 'java'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

defaultTasks 'build'

group 'eu.decentsoftware.holograms'
version '2.8.4'
description 'A lightweight yet very powerful hologram plugin with many features and configuration options.'

repositories {
    mavenCentral()
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://repo.codemc.io/repository/nms/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.dmulloy2.net/repository/public/' }
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.2'
    compileOnly 'com.arcaniax:HeadDatabase-API:1.3.1'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.8.0'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    compileOnly 'com.mojang:authlib:3.11.50'
    compileOnly 'commons-lang:commons-lang:2.6'
    compileOnly 'io.netty:netty-all:4.1.94.Final'

    // Annotations
    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
    compileOnly 'org.jetbrains:annotations:24.0.0'
    annotationProcessor 'org.jetbrains:annotations:24.0.0'
}

sourceCompatibility = '17'
targetCompatibility = '17'

compileJava {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.java
            artifactId 'decentholograms'
        }
    }
}

processResources {
    def props = [version: version, description: project.description]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    relocate "org.bstats", "eu.decentsoftware.holograms.metrics"
}